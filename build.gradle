buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.10.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.ben-manes.versions'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.gradle.plugin-publish'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'com.avast.gradle'
archivesBaseName = 'gradle-docker-compose-plugin'
version = '1'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile 'org.yaml:snakeyaml:1.23'
    testCompile gradleTestKit()
    testCompile ('org.spockframework:spock-core:1.1-groovy-2.4') {
        // Conflicting module versions: 2.4.7 vs. 2.4.1
        exclude group: 'org.codehaus.groovy'
    }
}

test {
    testLogging {
        events 'failed'
        exceptionFormat 'full'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'gradle-docker-compose-plugin'
        desc = 'Simplifies usage of Docker Compose for local development and integration testing in Gradle environment.'
        userOrg = 'avast'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/avast/gradle-docker-compose-plugin.git'
        websiteUrl = 'https://github.com/avast/gradle-docker-compose-plugin'
        issueTrackerUrl = 'https://github.com/avast/gradle-docker-compose-plugin/issues'
        labels = ['docker', 'docker-compose', 'gradle']
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}

pluginBundle {
    website = 'https://github.com/RussianPrussian/gradle-docker-compose-plugin'
    vcsUrl = 'https://github.com/RussianPrussian/gradle-docker-compose-plugin'
    description = 'Simplifies usage of Docker Compose for integration testing in Gradle environment. Extension of the avast plugin'
    tags = ['docker', 'docker-compose', 'gradle']
    plugins {
        dockerComposePlugin {
            id = 'com.russianprussian.avast.gradle.docker-compose'
            displayName = 'Gradle Docker Compose plugin extended from avast version'
			description = 'A plugin to simplify use and integration of docker-compose'
        }
    }
}
